<програма> ::= <сп.ог> { <сп.оп1> }
<сп.оп1> ::= <сп.оп>
<сп.ог> ::= <ог> | <сп.ог> ¶ <ог>
<ог> ::= <тип> <сп.ід1>
<сп.ід1> ::= <сп.ід>
<сп.ід> ::= @ ід | <сп.ід> , ід
<тип> ::= int | float
<сп.оп> ::= <оп> | <сп.оп> ¶ <оп>
<оп> ::= <ввід> | <вивід> | <цикл> | <ум.перехід> | <присв>
<ввід> ::= cin >> ід | <ввід> >> ід
<вивід> ::= cout << ід | <вивід> << ід
<цикл> ::= for ід = <вираз1> by <вираз1> to <вираз1> do <сп.оп1> rof
<вираз1> ::= <вираз>
<ум.перехід> ::= if <лог.вираз1> then <сп.оп1> fi
<присв> ::= ід = <вираз1> | ід = <терн>
<терн> ::= ? <лог.вираз1> ? <вираз1> : <вираз1>
<вираз> ::= <T1> | <вираз> + <T1> | <вираз> - <T1>
<T1> ::= <T>
<T> ::= <T> * <F> | <T> / <F> | <F>
<F> ::= ід | кфт | ( <вираз1> )
<лог.вираз> ::= <LT> | <LT> OR <лог.вираз>
<LT> ::= <LT> AND <LF> | <LF>
<LF> ::= NOT <LF> | [ <лог.вираз1> ] | <RL>
<лог.вираз1> ::= <лог.вираз>
<RL> ::= <вираз> <SG> <вираз1>
<SG> ::= > | >= | < | <= | != | ==